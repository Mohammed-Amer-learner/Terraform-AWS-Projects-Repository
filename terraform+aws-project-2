provider "aws" {
  region = "us-east-1"  
}

variable "cidr" {
  default = "10.0.0.0/16"
}

variable "private_key_path" {
  default = "~/.ssh/id_rsa"  
}

variable "public_key_path" {
  default = "~/.ssh/id_rsa.pub"  
}

resource "aws_key_pair" "example" {
  key_name   = "terr_project1"  
  public_key = file(var.public_key_path)
}

resource "aws_vpc" "terr_project1" {
  cidr_block = var.cidr
  tags = {
    Name = "terr_project1"
  }
}

resource "aws_subnet" "sub1" {
  vpc_id                  = aws_vpc.terr_project1.id
  cidr_block              = "10.0.0.0/24"
  availability_zone       = "us-east-1a"
  map_public_ip_on_launch = true
  tags = {
    Name = "PublicSubnet"
  }
}

resource "aws_internet_gateway" "igw" {
  vpc_id = aws_vpc.terr_project1.id
  tags = {
    Name = "InternetGateway"
  }
}

resource "aws_route_table" "RT" {
  vpc_id = aws_vpc.terr_project1.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.igw.id
  }

  tags = {
    Name = "PublicRouteTable"
  }
}

resource "aws_route_table_association" "rta1" {
  subnet_id      = aws_subnet.sub1.id
  route_table_id = aws_route_table.RT.id
}

resource "aws_security_group" "terr_sg" {
  name   = "terr_sg"
  vpc_id = aws_vpc.terr_project1.id

  ingress {
    description = "HTTP from VPC"
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
  ingress {
    description = "SSH"
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "TerrSecurityGroup"
  }
}

resource "aws_instance" "server" {
  ami                    = "ami-0261755bbcb8c4a84"
  instance_type          = "t2.micro"
  key_name               = aws_key_pair.example.key_name
  vpc_security_group_ids = [aws_security_group.terr_sg.id]
  subnet_id              = aws_subnet.sub1.id

  connection {
    type        = "ssh"
    user        = "ubuntu"  # Replace with the appropriate username for your EC2 instance
    private_key = file(var.private_key_path)
    host        = self.public_ip
  }

  # File provisioner to copy a file from local to the remote EC2 instance
  provisioner "file" {
    source      = "app.py"  # Replace with the path to your local file
    destination = "/home/ubuntu/app.py"  # Replace with the path on the remote instance
  }

  provisioner "remote-exec" {
    inline = [
      "echo 'Starting instance setup'",
      "sudo apt update -y",
      "sudo apt-get install -y python3-pip python3-venv",
      "python3 -m venv /home/ubuntu/myenv",
      "/home/ubuntu/myenv/bin/pip install flask || echo 'Flask installation failed'",
      "nohup /home/ubuntu/myenv/bin/python3 /home/ubuntu/app.py > /home/ubuntu/app.log 2>&1 &",
      "echo 'Instance setup complete'"
    ]
  }

  tags = {
    Name = "terr_project2"
  }
}



### EC2 instance is running, now go to terminal to see if the app.py file is running, open a new tab and enter ssh -i ~/.ssh/id_rsa ubuntu@paste ec2 public ip


Hereâ€™s a short explanation of the Terraform code:

    Provider Configuration:
        Specifies AWS as the cloud provider and the region (us-east-1) where resources will be created.

    Variables:
        Defines reusable values like the VPC CIDR block and paths for private/public SSH keys.

    Key Pair:
        Creates an AWS key pair using the public SSH key, allowing secure access to the EC2 instance.

    VPC (Virtual Private Cloud):
        Creates a private network (terr_project1) with the defined CIDR block for organizing resources.

    Subnet:
        Creates a public subnet (10.0.0.0/24) within the VPC and assigns public IPs to instances launched here.

    Internet Gateway:
        Enables internet connectivity for resources within the VPC..

    Route Table and Association:
        Configures a route to forward traffic from the public subnet to the internet via the gateway.

    Security Group:
        Creates a security group (terr_sg) to allow HTTP (port 80) and SSH (port 22) traffic from any source.

    EC2 Instance:
        Launches a t2.micro instance with a specified AMI. The instance:
            Belongs to the public subnet and the security group.
            Uses the generated key pair for secure access.

    Provisioners:
        File Provisioner: Copies the app.py file from the local machine to the EC2 instance.
        Remote-Exec Provisioner: Executes commands on the instance:
            Updates the system.
            Installs Python packages, including Flask.
            Runs app.py in the background, logging output to a file.

    Tags:
        Adds descriptive tags to resources for easy identification.
